 psect system_a,0,0,2,0,0

* Converted to new ABI on December 3, 2005

strsiz set 82 80 + cr + null

*    system(s)
*    char *s;
system:
 pshs d,u
 ldd #-166
 lbsr _stkcheck
 leas -(strsiz+4),s extra for status, cid
 ldd #strsiz-1 max size to copy
 ldu strsiz+4,s *s
 leax ,s *local_buffer
 pshs d
 pshs u
 tfr x,d
 lbsr strncpy
 leas 4,s
 clr strsiz-2,s in case string too long
 leax tcr,pcr
 pshs x d has string start
 lbsr strcat terminate with CR
 leas 2,s
 clra
 clrb
 pshs d data size
 incb
 pshs d type
 pshs d language
 leax 6,s parm pnt
 pshs x
 tfr x,d again for strlen
 lbsr strlen
 pshs d parm size
 leax tshell,pcr
 tfr x,d
 lbsr os9fork
 leas 10,s
 std strsiz,s save id
waitloop leax strsiz+2,s
 tfr x,d
 lbsr wait
 cmpd strsiz,s was it the right child?
 bne waitloop
 leas (strsiz+2),s waste buffer and cid
 puls d
 puls x,u,pc return status in d

tshell fcc 'shell'
tcr fcb $d,0

 endsect

 psect memcmp_a,0,0,2,0,0

* Converted to new ABI on December 3, 2005

* /*
*  * Compare n bytes.
*  * Return *s1 - *s2.
*  *  This amounts to s1 >, ==, or < s2.
*  */
*
* memcmp(s1, s2, n)
* register char *s1, *s2;
* int           n;
*    {
*    int   diff;
*
*    if (s1 != s2)
*       while (n-- > 0)
*          if (diff = *s1++ - *s2++)
*             return (diff);
*    return (0);
*    }

memcmp: pshs y,u
 tfr d,x s1
 cmpx 6,s
 beq memcmp3 if same
 ldu 6,s s2
 ldy 8,s n
 beq memcmp3 if null length

memcmp1 ldb ,u+ s2 char
 subb ,x+ *s2 - *s1
 beq memcmp2 if match
 negb fix sign
 sex
 bra memcmp4

memcmp2 leay -1,y
 bne memcmp1 more to go

memcmp3 clra
 clrb
memcmp4 puls y,u,pc

 endsect

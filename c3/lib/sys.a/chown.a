* ifp1
* use ../../defs/os9defs.a
* endc
*Carry equ %00000001
Write. equ 2 write access
FD.Att equ 0 FD offset to attr
FD.Own equ 1 FD offset to owner

Bufsize equ 16 FD info bytes

 psect chown,0,0,2,0,0

* chown(fname,owner)
chown:
 pshs y,u save registers
 leas -Bufsize,s reserve buffer space

 bsr openfile open file and read FD
 bcs chexit bra if error
 pshs a save path number

 os9 F$ID get user ID
 bcs chexit01 bra if error
 ldb #E$FNA prime for error
 cmpy #0 superuser?
 orcc #Carry prime the carry
 bne chexit01 bra if not


 ldd Bufsize+7,s get the new owner id
 std FD.Own,x modify the FD
 puls a restore path number
 ldb #SS.FD write FD code
 os9 I$SetStt write the FD
 bcs chexit bra if error
 os9 I$Close close the file

* carry set is error exit, clear for normal
chexit
 leas Bufsize,s return scratch
 puls y,u restore regs
 lbra _sysret return to caller
chexit01
 puls a
 bra chexit

*
* open file and fetch file FD
* Bufsize buffer is at 2,s
*
openfile
 tfr d,x address of name
 lda #Write. access mode
 os9 I$Open open the file
 bcc openf10 exit if error
 rts

openf10
 leax 2,s get buffer address
 ldy #Bufsize and size
 ldb #SS.FD read FD code
 os9 I$GetStt read the FD
 rts

 endsect
